sql {
	Expression = ExpressionSelect | ExpressionOr
	ExpressionSelect = "select" ListOf<ExpressionSelectField, ","> ("from" ListOf<ExpressionSelectTable, ",">)?
	ExpressionSelectField = Expression identifier?
	ExpressionSelectTable = Expression identifier?
	ExpressionOr =
		ExpressionOr "||" ExpressionAnd	-- or
		| ExpressionAnd
	ExpressionAnd =
		ExpressionAnd "&&" ExpressionNot	-- and
		| ExpressionNot
	ExpressionNot =
		"!" ExpressionNot	-- not
		| ExpressionRelation
	ExpressionRelation =
		ExpressionRelation ("<="|"="|">="|"<"|"!="|">"|"in") ExpressionAdd	-- relation
		| ExpressionAdd
	ExpressionAdd =
		ExpressionAdd ("+"|"-") ExpressionMultiply	-- add
		| ExpressionMultiply
	ExpressionMultiply =
		ExpressionMultiply ("*"|"/") ExpressionAddUnary	-- multiply
		| ExpressionAddUnary
	ExpressionAddUnary =
		("+"|"-") ExpressionAddUnary	-- add
		| ExpressionAtom
	ExpressionAtom =
		"(" Expression ")"	-- parentheses
		| ExpressionName
		| number
		| string
	ExpressionName = (identifier ".")? identifier
	identifier = ~(("select"|"from") ~(alnum|"_")) &(letter|"_") (alnum|"_")+
	number = digit+ ("." digit+)?
	string = "\"" char* "\""
	char = ~("\""|"\\") any	-- literal
		| "\\" ("\"" | "\\" | "b" | "f" | "n" | "r" | "t" | "v")	-- escaped
}